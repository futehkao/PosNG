ext.posngVersion = '1.1.1'
ext.log4jVersion = '2.13.3'
ext.slf4jVersion = '1.7.30'
ext.log4jVersion = '2.13.3'
ext.jacksonVersion = '2.11.0'

ext.junitPlatformLauncherVersion = '1.6.2'
ext.junitJupiterVersion = '5.6.2'

allprojects {
    group 'com.futeh.PosNG'
    version "${posngVersion}-SNAPSHOT"

    apply plugin: 'java'

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:syntax', '-quiet')
    }

    repositories {
        mavenCentral()
    }
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

subprojects {

    group 'com.futeh.PosNG'
    version "${posngVersion}"

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'signing'

    archivesBaseName = "PosNG-${project.name}"

    signing {
        sign configurations.archives
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compile
    }

    task sourcesJar(type: Jar) {
        archiveBaseName.set "PosNG-${project.name}"
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            common(MavenPublication) {
                from components.java
                artifactId "PosNG-${project.name}"

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    description "PosNG-${project.name}"
                    packaging 'jar'
                    // optionally artifactId can be defined here. description 'A application used as an example on how to set up pushing its components to the Central Repository.'
                    url 'http://github.com/futehkao/PosNG.git'

                    scm {
                        connection 'scm:git:https://github.com/futehkao/PosNG.git'
                        developerConnection 'scm:git:https://github.com/futehkao/PosNG.git'
                        url 'https://github.com/futehkao/PosNG.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'futeh'
                            name 'Futeh Kao'
                            email 'futeh@yahoo.com'
                        }
                    }
                }
            }
        }
    }

}
